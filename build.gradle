buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'

minecraft {
    version = "1.8-11.14.1.1313"
    replace "@MODVERSION@", project.version
    replaceIn "m.java"
    
    // http://export.mcpbot.bspk.rs/stable/
    mappings = "stable_16"
}


task deleteOldArtifacts(type: Delete) {
    delete 'build/libs'
}

classes.dependsOn deleteOldArtifacts

group = 'modmuss50'
archivesBaseName = "mod16K"

def ENV = System.getenv()
if (ENV.BUILD_NUMBER) {
    version = "0.1." + "${System.getenv().BUILD_NUMBER}"
} else {
    version = "0.1.BUILD"
}

processResources
        {
            // this will ensure that this task is redone when the versions change.
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            // replace stuff in mcmod.info, nothing else
            from(sourceSets.main.resources.srcDirs) {
                include '*.info'

                // replace version and mcversion
                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            // copy everything else, thats not the mcmod.info
            from(sourceSets.main.resources.srcDirs) {
                exclude '**/*.info'
            }
        }

jar {
    exclude "**/*.psd"
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
    classifier = 'src'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'dev'
    exclude "**/*.psd"
    classifier = 'dev'
}

jar {
    exclude "**/*.psd"
    appendix = 'universal'
    classifier = 'universal'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file:///var/www/maven/")
            pom {
                groupId = "16KB-Mod"
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description '16KB-Mod'
                    url 'https://github.com/modmuss50/16KB-Mod'
                    scm {
                        url 'https://github.com/modmuss50/16KB-Mod'
                        connection 'scm:git:git@github.com:modmuss50/16KB-Mod.git'
                        developerConnection 'scm:git:git@github.com:modmuss50/16KB-Mod.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/modmuss50/16KB-Mod/issues'
                    }
                    licenses {
                        license {
                            name 'License'
                            url 'https://github.com/modmuss50/16KB-Mod/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'modmuss50'
                            name 'modmuss50'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
